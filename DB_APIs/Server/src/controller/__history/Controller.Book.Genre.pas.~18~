Unit Controller.Book.Genre;

Interface

Uses
    MVCFramework,
    MVCFramework.Commons,
    MVCFramework.Serializer.Commons,
    System.SysUtils,
    System.Generics.Collections,
    Service.Book.Genre,
    WebModule.Main,
    Model.Book.Genre;

Type
    [MVCPath(BASE_API_V1 + '/genres')]
    TGenreController = class(TMVCController)
    Private
      FGenreService: IGenreService;

    Public
        Constructor Create; override;
        Destructor Destroy; override;

        [MVCPath('')]
        [MVCHTTPMethod([httpGET])]
        Procedure GetAllGenres();

        [MVCPath('')]
        [MVCHTTPMethod([httpPOST])]
        Procedure CreateGenre([MVCFromBody] const AGenre: TGenre);

        [MVCPath('')]
        [MVCHTTPMethod([httpPUT])]
        Procedure UpdateGenre([MVCFromBody] const AGenre: TGenre);

        [MVCPath('/($id)')]
        [MVCHTTPMethod([httpGET])]
        Procedure GetGenreByID(id: SmallInt);

        [MVCPath('/($id)')]
        [MVCHTTPMethod([httpDELETE])]
        Procedure DeleteGenre(id: SmallInt);
End;

Implementation

{ TGenreController }

//______________________________________________________________________________
Constructor TGenreController.Create;
Begin
    Inherited;
    FGenreService := TGenreService.Create;
End;
//______________________________________________________________________________
destructor TGenreController.Destroy;
Begin
    FGenreService := nil;
    Inherited;
End;
//______________________________________________________________________________
Procedure TGenreController.GetAllGenres;
Var
    Genres: TObjectList<TGenre>;
Begin
    Genres := FGenreService.GetAllGenres;
    Render(Genres);
End;
//______________________________________________________________________________
Procedure TGenreController.GetGenreByID(id: SmallInt);
Var
    Genre: TGenre;
Begin
    Genre := FGenreService.GetGenreByID(id);
    If Assigned(Genre) then
        Render(Genre)
    Else
        Render(HTTP_STATUS.NotFound, 'Genre not found');
End;
//______________________________________________________________________________
Procedure TGenreController.CreateGenre(const AGenre: TGenre);
Begin
    FGenreService.AddGenre(AGenre);
    Render(HTTP_STATUS.Created, 'Add Successfully');
End;
//______________________________________________________________________________
Procedure TGenreController.UpdateGenre(Const AGenre: TGenre);
Begin
    FGenreService.UpdateGenre(AGenre);
    Render(AGenre);
End;
//______________________________________________________________________________
Procedure TGenreController.DeleteGenre(id: SmallInt);
Begin
    FGenreService.DeleteGenre(id);
    Render(HTTP_STATUS.OK, 'Delete Successfully');
End;
//______________________________________________________________________________

End.

