Unit Controller.Book.CollectionFull;

Interface

Uses
    MVCFramework, MVCFramework.Commons, System.SysUtils,
    JsonDataObjects,
    Service.Book.CollectionFull;

Type
    [MVCPath('/collection')]
    TBookCollectionFullController = class(TMVCController)
    Private
        FService: ICollectionFullService;
    Public
        Constructor Create; override;
        Destructor Destroy; override;

        [MVCPath('')]
        [MVCHTTPMethod([httpGET])]
        Procedure GetCollections;

        [MVCPath('/full')]
        [MVCHTTPMethod([httpGET])]
        Procedure GetCollectionFullByID([MVCFromQueryString('collectionid', 0)] id: Integer);

        [MVCPath('/full')]
        [MVCHTTPMethod([httpPOST])]
        Procedure AddCollectionFull;

        [MVCPath('/full')]
        [MVCHTTPMethod([httpPUT])]
        Procedure UpdateCollectionFull([MVCFromQueryString('collectionid', 0)] id: Integer);

        [MVCPath('/full')]
        [MVCHTTPMethod([httpDELETE])]
        Procedure DeleteCollectionFull([MVCFromQueryString('collectionid', 0)] id: Integer);
End;

Implementation

Uses
    MVCFramework.Serializer.Commons,
    System.Generics.Collections;

//______________________________________________________________________________
Constructor TBookCollectionFullController.Create();
Begin
    Inherited;
    FService := TCollectionFullService.Create;
End;
//______________________________________________________________________________
Destructor TBookCollectionFullController.Destroy();
Begin
    FService := nil;
    Inherited;
End;
//______________________________________________________________________________
Procedure TBookCollectionFullController.GetCollections;
Begin
    Render(FService.GetAll);
End;
//______________________________________________________________________________
Procedure TBookCollectionFullController.GetCollectionFullByID(id: Integer);
var
  JSON: TJSONObject;
begin
  JSON := FService.GetFullByID(id);
  if Assigned(JSON) then
    Render(JSON)
  else
    Render(HTTP_STATUS.NotFound, 'Collection not found');
end;

Procedure TBookCollectionFullController.AddCollectionFull;
var
  JSONBody: TJSONObject;
  NewID: Int64;
begin
  JSONBody := ParseJSONObject(Request.Body);
  try
    NewID := FService.AddFull(JSONBody);
    Render201('/collections/full/' + NewID.ToString, 'Created');
  finally
    JSONBody.Free;
  end;
end;

Procedure TBookCollectionFullController.UpdateCollectionFull(id: Integer);
var
  JSONBody: TJSONObject;
begin
  JSONBody := ParseJSONObject(Request.Body);
  try
    FService.UpdateFull(id, JSONBody);
    Render204;
  finally
    JSONBody.Free;
  end;
end;

Procedure TBookCollectionFullController.DeleteCollectionFull(id: Integer);
begin
  FService.DeleteFull(id);
  Render204;
end;

end.

